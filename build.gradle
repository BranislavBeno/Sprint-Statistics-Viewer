plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'com.gorylenko.gradle-git-properties' version '2.4.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

group = 'com.sprint'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

ext {
    set('sonarProjectName', "SprintStatsViewer") as Object
    set('sonarProjectKey', "com.view.sprintstats") as Object
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

var seleniumVersion = '4.1.2'

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "io.micrometer:micrometer-registry-prometheus:1.8.3"
    implementation "de.codecentric:spring-boot-admin-starter-client:2.6.2"
    implementation "org.springframework:spring-jdbc"
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation "mysql:mysql-connector-java"
    implementation "guru.springframework:chuck-norris-for-actuator:2.4.0"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:selenium"
    testImplementation "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:${seleniumVersion}"
    testImplementation "com.codeborne:selenide:6.3.3"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.16.3"
    }
}


springBoot {
    buildInfo()
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_SprintStatsViewer"
    }
}

bootBuildImage {
    imageName = "beo1975/sprint-stats-viewer:${versionMajor}.${versionMinor}.${versionPatch}"
}