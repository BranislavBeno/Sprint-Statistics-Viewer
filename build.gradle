plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

group = 'com.sprint'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

ext {
    sonarProjectName = 'SprintStatsViewer'
    sonarProjectKey = 'com.view.sprintstats'
    jacocoDestinationFile = file("$buildDir/jacoco/test.exec")
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file(jacocoDestinationFile)
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "io.micrometer:micrometer-registry-prometheus:1.8.0"
    implementation "de.codecentric:spring-boot-admin-starter-client:2.5.4"
    implementation "org.springframework:spring-jdbc"
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation "mysql:mysql-connector-java"
    implementation "guru.springframework:chuck-norris-for-actuator:2.4.0"
    implementation platform("org.testcontainers:testcontainers-bom:1.16.2")
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:selenium"
    testImplementation "org.assertj:assertj-core"
    testImplementation "com.codeborne:selenide:6.0.3"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 8
version = "R${versionMajor}.${versionMinor}.${versionPatch}"

springBoot {
    buildInfo()
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_SprintStatsViewer"
    }
}

// Docker image building
bootBuildImage {
    imageName = "beo1975/sprint-stats-viewer:${versionMajor}.${versionMinor}.${versionPatch}"
}