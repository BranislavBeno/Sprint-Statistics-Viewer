plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

group = 'com.sprint'

def versionMajor = 1
def versionMinor = 2
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

ext {
    set('sonarProjectName', "SprintStatsViewer") as Object
    set('sonarProjectKey', "com.view.sprintstats") as Object
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "io.micrometer:micrometer-registry-prometheus:1.11.1"
    implementation "org.springframework:spring-jdbc"
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation "com.mysql:mysql-connector-j"
    implementation "org.yaml:snakeyaml:2.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "com.codeborne:selenide:6.15.0"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:selenium"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.18.3"
    }
}

bootJar {
    archiveFileName = "sprint-stats-viewer.jar"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

springBoot {
    buildInfo()
}

jacoco {
    toolVersion = '0.8.10'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_SprintStatsViewer"
        property "sonar.projectName", "sprint-stats-viewer"
    }
}

bootBuildImage {
    imageName = "beo1975/sprint-stats-viewer:${versionMajor}.${versionMinor}.${versionPatch}"
}
